[HEADER]
	VERSION		01.00.00.00
	DESCRIPTION OGLES3DeferredShading
	COPYRIGHT	Imagination Technologies
[/HEADER]

///////////////////////////////////////////////////////////////////////////
////                                                                   ////
////                   GBUFFER RENDER                                  ////
////                                                                   ////
///////////////////////////////////////////////////////////////////////////


[VERTEXSHADER]
	NAME 		GBufferVertexShader

	[GLSL_CODE]
		attribute highp   vec3 inVertex;
		attribute highp   vec3 inNormal;
		attribute mediump vec2 inTexCoord;
		attribute highp   vec3 inTangent;

		uniform  mat4  uWorldViewProjMatrix; 
		uniform  mat4  uWorldViewMatrix; 
		uniform  mat4  uViewProjMatrix;
		uniform  mat4  uViewMatrix;
		uniform  mat3  uWorldViewIT; 

		varying  mediump  vec2   vTexCoord;
		varying  highp    vec3   vNormal;
		varying  highp    vec3   vTangent;
		varying  highp    vec3   vBinormal;
		varying  highp    vec3   vViewPos;

		void main() 
		{
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
	  
			// Transform normal from model space to eye space
			vNormal = uWorldViewIT * inNormal;
			vTangent = uWorldViewIT * inTangent;
			vBinormal = cross(vNormal, vTangent);
	  
			// Pass the vertex position in view space for depth calculations
			vViewPos = (uWorldViewMatrix * vec4(inVertex, 1.0)).xyz;
	
			// Pass the texture coordinates to the fragment shader
			vTexCoord = inTexCoord;				
		}
	[/GLSL_CODE]
[/VERTEXSHADER]
 
//
// Effect to render the scene using a cube shadow map
//
[EFFECT] 
	NAME 	RenderAlbedo
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix		WORLDVIEW
	UNIFORM uViewProjMatrix			VIEWPROJECTION
	UNIFORM uViewMatrix				VIEW
	UNIFORM uWorldViewIT		 	WORLDVIEWIT
		
	UNIFORM sTexture				TEXTURE0
	UNIFORM sBumpMap				TEXTURE1
	
	// ATTRIBUTES
	ATTRIBUTE inVertex				POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV
	ATTRIBUTE	inTangent			TANGENT
	ATTRIBUTE	inBoneIndex			BONEINDEX
	ATTRIBUTE	inBoneWeights		BONEWEIGHT
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
		
	UNIFORM uSpecularPower				CUSTOMSEMANTIC_SPECULARPOWER
	UNIFORM uDiffuseColour				CUSTOMSEMANTIC_DIFFUSECOLOUR
		
	// SHADERS
	VERTEXSHADER   GBufferVertexShader
	FRAGMENTSHADER AlbedoFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		AlbedoFragmentShader 

	[GLSL_CODE]
		uniform  sampler2D  sTexture;
		uniform  sampler2D  sBumpMap;

		uniform  highp float  uFarClipDistance;
		uniform  lowp  float  uSpecularPower;
		uniform  lowp  vec3   uDiffuseColour;

		varying  mediump vec2   vTexCoord;
		varying  highp   vec3   vNormal;
		varying  highp   vec3   vTangent;
		varying  highp   vec3   vBinormal;
		varying  highp   vec3   vViewPos;
		
		void main() 
		{
			// Calculate the albedo
			lowp vec3 albedo = texture2D(sTexture, vTexCoord).rgb * uDiffuseColour;
			// Pack the specular exponent with the albedo
			gl_FragColor = vec4(albedo, uSpecularPower);
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

//
// Effect to render the scene using a cube shadow map
//
[EFFECT] 
	NAME 	RenderNormals
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix		WORLDVIEW
	UNIFORM uViewProjMatrix			VIEWPROJECTION
	UNIFORM uViewMatrix				VIEW
	UNIFORM uWorldViewIT		 	WORLDVIEWIT
		
	UNIFORM sTexture				TEXTURE0
	UNIFORM sBumpMap				TEXTURE1
	
	// ATTRIBUTES
	ATTRIBUTE inVertex				POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV
	ATTRIBUTE	inTangent			TANGENT
	ATTRIBUTE	inBoneIndex			BONEINDEX
	ATTRIBUTE	inBoneWeights		BONEWEIGHT
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
		
	UNIFORM uSpecularPower				CUSTOMSEMANTIC_SPECULARPOWER
	UNIFORM uDiffuseColour				CUSTOMSEMANTIC_DIFFUSECOLOUR
		
	// SHADERS
	VERTEXSHADER   GBufferVertexShader
	FRAGMENTSHADER NormalFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		NormalFragmentShader 

	[GLSL_CODE]
		uniform  sampler2D  sBumpMap;

		uniform  highp float  uFarClipDistance;
		uniform  lowp  float  uSpecularPower;
		uniform  lowp  vec3   uDiffuseColour;

		varying  mediump vec2   vTexCoord;
		varying  highp   vec3   vNormal;
		varying  highp   vec3   vTangent;
		varying  highp   vec3   vBinormal;
		varying  highp   vec3   vViewPos;

		void main() 
		{
			// Calculate viewspace perturbed normal
			highp vec3 bumpmap = normalize(texture2D(sBumpMap, vTexCoord).rgb * 2.0 - 1.0);
			highp mat3 tangentSpace = mat3(normalize(vTangent), normalize(vBinormal), normalize(vNormal));	
			highp vec3 normalVS = tangentSpace * bumpmap;		

			// Scale the normal range from [-1,1] to [0, 1] to pack it into the RGB_U8 texture
			gl_FragColor.xyz = normalVS * 0.5 + 0.5;	

		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

//
// Effect to render the scene depth and split it up into RGBA channels
//
[EFFECT] 
	NAME 	RenderDepthChannelSplit
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix		WORLDVIEW
	UNIFORM uViewProjMatrix			VIEWPROJECTION
	UNIFORM uViewMatrix				VIEW
	UNIFORM uWorldViewIT		 	WORLDVIEWIT
		
	UNIFORM sTexture				TEXTURE0
	UNIFORM sBumpMap				TEXTURE1
	
	// ATTRIBUTES
	ATTRIBUTE inVertex				POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV
	ATTRIBUTE	inTangent			TANGENT
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
		
	UNIFORM uSpecularPower				CUSTOMSEMANTIC_SPECULARPOWER
	UNIFORM uDiffuseColour				CUSTOMSEMANTIC_DIFFUSECOLOUR
		
	// SHADERS
	VERTEXSHADER   GBufferVertexShader
	FRAGMENTSHADER ChannelSplitDepthFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		ChannelSplitDepthFragmentShader 

	[GLSL_CODE]
		uniform  highp float  uFarClipDistance;

		varying  highp   vec3   vViewPos;
				
		void main() 
		{		
			// Pack the depth value into 4 channels 	
			highp float scaledDepth = length(vViewPos) / uFarClipDistance;
			highp vec4 enc = vec4(1.0, 255.0, 65025.0, 160581375.0) * scaledDepth;
			enc = fract(enc);
			enc -= enc.yzww * vec4(1.0/255.0,1.0/255.0,1.0/255.0,0.0);			
			
			gl_FragColor = enc;
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

//
// Effect to render the scene depth and exaggerate the actual depth
//
[EFFECT] 
	NAME 	RenderDepth
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix		WORLDVIEW
	UNIFORM uViewProjMatrix			VIEWPROJECTION
	UNIFORM uViewMatrix				VIEW
	UNIFORM uWorldViewIT		 	WORLDVIEWIT
		
	UNIFORM sTexture				TEXTURE0
	UNIFORM sBumpMap				TEXTURE1
	
	// ATTRIBUTES
	ATTRIBUTE inVertex				POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV
	ATTRIBUTE	inTangent			TANGENT
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
		
	UNIFORM uSpecularPower				CUSTOMSEMANTIC_SPECULARPOWER
	UNIFORM uDiffuseColour				CUSTOMSEMANTIC_DIFFUSECOLOUR
		
	// SHADERS
	VERTEXSHADER   GBufferVertexShader
	FRAGMENTSHADER DepthFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		DepthFragmentShader 

	[GLSL_CODE]
		uniform  highp float  uFarClipDistance;

		varying  highp   vec3   vViewPos;
				
		void main() 
		{		
			// Pack the depth value into 4 channels 	
			highp float scaledDepth = length(vViewPos) / uFarClipDistance;
			gl_FragColor = vec4(vec3(scaledDepth * 5.0), 1.0);
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

///////////////////////////////////////////////////////////////////////////
////                                                                   ////
////                   DEFERRED LIGHT SHADERS                          ////
////                                                                   ////
///////////////////////////////////////////////////////////////////////////

// 
// Vertex shader that is shared amongst (most) deferred light shaders
//
[VERTEXSHADER]
	NAME 		DeferredVertexShader

	[GLSL_CODE]
		attribute highp   vec3 inVertex;

		uniform  mat4  uWorldViewProjMatrix;
		uniform  mat4  uWorldViewMatrix;
		
		varying  highp   vec3  vPositionVS;
		varying  highp   vec3  vViewDirVS;
		varying  mediump vec2  vTexCoord;

		void main() 
		{ 	
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
			gl_Position.xyz = gl_Position.xyz / gl_Position.w;
			gl_Position.w = 1.0;
			
			vTexCoord = (gl_Position.xy + 1.0) * 0.5;
	
			// Calculate the view-space position for lighting calculations
			vPositionVS = (uWorldViewMatrix * vec4(inVertex, 1.0)).xyz;

			// Pass the view direction
			vViewDirVS = vPositionVS;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

//
// Calculates the lighting for a point light source
//
[EFFECT] 
	NAME 	RenderPointLight
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix		WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix			WORLDVIEW
	UNIFORM uWorldIT					WORLDIT
	UNIFORM uLightColourIntensity		LIGHTCOLOR
		
	UNIFORM sAlbedo						TEXTURE0
	UNIFORM sNormals					TEXTURE1
	UNIFORM sDepth						TEXTURE2
	UNIFORM sLightEnvMap				TEXTURE3
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
	
	UNIFORM uLightViewPosition		CUSTOMSEMANTIC_POINTLIGHT_VIEWPOSITION
	
	// ATTRIBUTES
	ATTRIBUTE inVertex			POSITION

	// SHADERS	
	VERTEXSHADER   DeferredVertexShader
	FRAGMENTSHADER PointLightFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		PointLightFragmentShader 

	[GLSL_CODE]
		uniform  sampler2D    sAlbedo;
		uniform  sampler2D    sNormals;
		uniform  sampler2D    sDepth;
		uniform  samplerCube  sLightEnvMap;

		uniform  highp float  uFarClipDistance;
		uniform  highp vec3   uLightColourIntensity;

		uniform  highp mat3   uWorldIT;		
		uniform  highp vec3   uLightViewPosition;
		
		varying  highp   vec3  vPositionVS;
		varying  highp   vec3  vViewDirVS;
		varying  mediump vec2  vTexCoord;

		void main()
		{
			//
			// Read GBuffer attributes
			//	
			highp vec4 depthTex = texture2D(sDepth, vTexCoord);
			highp vec4 albedoSpec = texture2D(sAlbedo, vTexCoord);
			highp vec3 normalTex = texture2D(sNormals, vTexCoord).xyz;
			
			// reconstruct original depth value
			highp float depth = dot(depthTex, vec4(1.0, 1.0/255.0, 1.0/65025.0, 1.0/160581375.0));
			
			//
			// Reconstruct common vectors and world space position 
			//
			highp vec3 positionVS = normalize(vPositionVS) * depth * uFarClipDistance;	
			highp vec3 lightDirection = uLightViewPosition - positionVS;
			lowp  vec3 lightColour = textureCube(sLightEnvMap, uWorldIT * lightDirection).rgb;
			highp float invLightDistance = 1.0 / length(lightDirection);
			lightDirection *= invLightDistance;
	
			//
			// Calculate lighting terms
			//
			highp vec3 normal = normalize(normalTex * 2.0 - 1.0);	
			highp float n_dot_l = max(dot(lightDirection, normal), 0.0);	
			highp vec3 diffuse = n_dot_l * albedoSpec.rgb;

			highp vec3 viewDirection = normalize(vViewDirVS);
			highp vec3 reflectedLightDirection = reflect(lightDirection, normal);
			highp float v_dot_r = max(dot(viewDirection, reflectedLightDirection), 0.0);
			diffuse += vec3(pow(v_dot_r, albedoSpec.a * 100.0));
	
			highp float attenuation = invLightDistance * invLightDistance;
			gl_FragColor = vec4(diffuse * uLightColourIntensity * attenuation * lightColour, 1.0);
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

//
// Calculates the lighting for a directional light source
//
[EFFECT] 
	NAME 	RenderDirectionalLight
		
	// GLOBALS UNIFORMS
	UNIFORM uLightColourIntensity		LIGHTCOLOR
			
	UNIFORM sAlbedo						TEXTURE0
	UNIFORM sNormals					TEXTURE1
		
	// CUSTOM SEMANTICS
	UNIFORM uLightDirection			CUSTOMSEMANTIC_DIRECTIONALLIGHT_DIRECTION
	
	// ATTRIBUTES
	ATTRIBUTE inVertex				POSITION
	ATTRIBUTE inTexCoord			UV

	// SHADERS	
	VERTEXSHADER   DirectionalLightVertexShader
	FRAGMENTSHADER DirectionalLightFragmentShader
[/EFFECT]

[VERTEXSHADER]
	NAME 		DirectionalLightVertexShader

	[GLSL_CODE]
		attribute highp   vec3 inVertex;
		attribute mediump vec2 inTexCoord;

		uniform  highp vec4  uLightDirection;

		varying  highp   vec3  vLightDirection;
		varying  mediump vec2  vTexCoord;

		void main() 
		{ 	
			gl_Position = vec4(inVertex, 1.0);

			vTexCoord = inTexCoord;
			vLightDirection = uLightDirection.xyz;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

[FRAGMENTSHADER] 
	NAME 		DirectionalLightFragmentShader 

	[GLSL_CODE]
		uniform  sampler2D  sAlbedo;
		uniform  sampler2D  sNormals;

		uniform highp vec3  uLightColourIntensity;

		varying highp   vec3 vLightDirection;
		varying mediump vec2 vTexCoord;

		void main()
		{
			// Fetch required gbuffer attributes
			lowp  vec3 albedo = texture2D(sAlbedo, vTexCoord).rgb;
			highp vec3 normalTex = texture2D(sNormals, vTexCoord).xyz;
			highp vec3 normal = normalize(normalTex.xyz * 2.0 - 1.0);
			
			// Calculate simple diffuse lighting
			highp float n_dot_l = max(dot(-vLightDirection, normal.xyz), 0.0);
			lowp vec3 colour = albedo * n_dot_l * uLightColourIntensity;
						
			gl_FragColor = vec4(colour, 1.0);			
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

///////////////////////////////////////////////////////////////////////////
////                                                                   ////
////                   AUXILIARY SHADERS                               ////
////                                                                   ////
///////////////////////////////////////////////////////////////////////////

//
// Renders the geometry using a texture
//
[EFFECT] 
	NAME 	RenderSimpleTexture
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix		WORLDVIEWPROJECTION
	
	UNIFORM sTexture					TEXTURE0
	
	// ATTRIBUTES
	ATTRIBUTE inVertex					POSITION
	ATTRIBUTE	inTexCoord				UV	

	// SHADERS	
	VERTEXSHADER   TextureVertexShader
	FRAGMENTSHADER TextureFragmentShader
[/EFFECT]

[VERTEXSHADER]
	NAME 		TextureVertexShader

	[GLSL_CODE]
		attribute highp   vec3 inVertex;
		attribute mediump vec2 inTexCoord;

		varying mediump vec2 vTexCoord;

		void main() 
		{ 
			// pass-through position and texture coordinates
			gl_Position = vec4(inVertex, 1.0);
			vTexCoord = inTexCoord;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

[FRAGMENTSHADER] 
	NAME 		TextureFragmentShader 

	[GLSL_CODE]
		uniform sampler2D sTexture;
		varying mediump vec2 vTexCoord;

		void main()
		{
			gl_FragColor = texture2D(sTexture, vTexCoord);			
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]


//
// Renders the geometry using a single channel depth map, 
// scaling the depth values for visualization purposes.
//
[EFFECT] 
	NAME 	RenderDepthTexture
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	
	UNIFORM sTexture				TEXTURE0
	
	// ATTRIBUTES
	ATTRIBUTE	inVertex			POSITION
	ATTRIBUTE	inTexCoord			UV	

	// SHADERS	
	VERTEXSHADER   TextureVertexShader
	FRAGMENTSHADER DepthTextureFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		DepthTextureFragmentShader 

	[GLSL_CODE]
		uniform sampler2D sTexture;
		varying mediump vec2 vTexCoord;

		void main()
		{
			highp vec4 depthTex = texture2D(sTexture, vTexCoord);
			highp float depth = dot(depthTex, vec4(1.0, 1.0/255.0, 1.0/65025.0, 1.0/160581375.0));
			gl_FragColor = vec4(vec3(depth * 5.0), 1.0);
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]


//
// Renders the geometry using a single colour passed as an uniform.
//
[EFFECT] 
	NAME 	RenderSolidColour
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uConstantColour			MATERIALCOLORAMBIENT
	
	// ATTRIBUTES
	ATTRIBUTE	inVertex			POSITION
	ATTRIBUTE	inTexCoord			UV

	// SHADERS	
	VERTEXSHADER   VertexShader
	FRAGMENTSHADER SolidColourFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		SolidColourFragmentShader 

	[GLSL_CODE]
		uniform lowp vec4 uConstantColour;

		void main()
		{
			gl_FragColor = uConstantColour;
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

[VERTEXSHADER]
	NAME 		VertexShader

	[GLSL_CODE]
		attribute highp   vec3 inVertex;
		attribute mediump vec2 inTexCoord;

		uniform  mat4  uWorldViewProjMatrix; 

		varying mediump vec2 vTexCoord;

		void main() 
		{ 
			// pass-through position and texture coordinates
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
			vTexCoord = inTexCoord;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

//
// Renders the geometry using a texture
//
[EFFECT] 
	NAME 	RenderCubeTexture
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldIT				WORLDIT
	UNIFORM uColour					MATERIALCOLORAMBIENT
	
	UNIFORM sTexture				TEXTURE0
	
	// ATTRIBUTES
	ATTRIBUTE	inVertex			POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV

	// SHADERS	
	VERTEXSHADER   CubeTextureVertexShader
	FRAGMENTSHADER CubeTextureFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		CubeTextureFragmentShader 

	[GLSL_CODE]
		uniform samplerCube sTexture;		
		uniform lowp vec4 uColour;
		
		varying mediump vec3 vNormal;
		
		void main()
		{
			gl_FragColor = textureCube(sTexture, vNormal) * uColour;
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

[VERTEXSHADER]
	NAME 		CubeTextureVertexShader

	[GLSL_CODE]
		attribute highp	vec3 inVertex;
		attribute highp	vec3 inNormal;

		uniform  highp mat4  uWorldViewProjMatrix; 
		uniform  highp mat3  uWorldIT;

		varying mediump vec3 vNormal;

		void main() 
		{ 
			// pass-through position and texture coordinates
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
			vNormal = uWorldIT * inNormal;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]