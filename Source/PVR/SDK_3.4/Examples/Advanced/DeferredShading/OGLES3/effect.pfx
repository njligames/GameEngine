[HEADER]
	VERSION		01.00.00.00
	DESCRIPTION OGLES3DeferredShading
	COPYRIGHT	Imagination Technologies
[/HEADER]

///////////////////////////////////////////////////////////////////////////
////                                                                   ////
////                   GBUFFER RENDER                                  ////
////                                                                   ////
///////////////////////////////////////////////////////////////////////////

//
// Effect to render the scene using a cube shadow map
//
[EFFECT] 
	NAME 	RenderGBuffer
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix		WORLDVIEW
	UNIFORM uViewProjMatrix			VIEWPROJECTION
	UNIFORM uViewMatrix				VIEW
	UNIFORM uWorldViewIT		 	WORLDVIEWIT
		
	UNIFORM sTexture				TEXTURE0
	UNIFORM sBumpMap				TEXTURE1
	
	// ATTRIBUTES
	ATTRIBUTE inVertex				POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV
	ATTRIBUTE	inTangent			TANGENT
	ATTRIBUTE	inBoneIndex			BONEINDEX
	ATTRIBUTE	inBoneWeights		BONEWEIGHT
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
		
	UNIFORM uSpecularPower				CUSTOMSEMANTIC_SPECULARPOWER
	UNIFORM uDiffuseColour				CUSTOMSEMANTIC_DIFFUSECOLOUR
		
	// SHADERS
	VERTEXSHADER   GBufferVertexShader
	FRAGMENTSHADER GBufferFragmentShader
[/EFFECT]

[VERTEXSHADER]
	NAME 		GBufferVertexShader

	[GLSL_CODE]
		#version 300 es

		#define VERTEX_ARRAY      0
		#define NORMAL_ARRAY      1
		#define TEXCOORD_ARRAY    2
		#define TANGENT_ARRAY     3
		#define BONEINDEX_ARRAY   4
		#define BONEWEIGHT_ARRAY  5
		
		layout(location = VERTEX_ARRAY)   in highp   vec3 inVertex;
		layout(location = NORMAL_ARRAY)   in highp   vec3 inNormal;
		layout(location = TEXCOORD_ARRAY) in mediump vec2 inTexCoord;
		layout(location = TANGENT_ARRAY)  in highp   vec3 inTangent;

		uniform  mat4  uWorldViewProjMatrix; 
		uniform  mat4  uWorldViewMatrix; 
		uniform  mat4  uViewProjMatrix;
		uniform  mat4  uViewMatrix;
		uniform  mat3  uWorldViewIT; 

		out  mediump  vec2   vTexCoord;
		out  highp    vec3   vNormal;
		out  highp    vec3   vTangent;
		out  highp    vec3   vBinormal;
		out  highp    vec3   vViewPos;

		void main() 
		{
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
	  
			// Transform normal from model space to eye space
			vNormal = uWorldViewIT * inNormal;
			vTangent = uWorldViewIT * inTangent;
			vBinormal = cross(vNormal, vTangent);
	  
			// Pass the vertex position in view space for depth calculations
			vViewPos = (uWorldViewMatrix * vec4(inVertex, 1.0)).xyz;
	
			// Pass the texture coordinates to the fragment shader
			vTexCoord = inTexCoord;				
		}
	[/GLSL_CODE]
[/VERTEXSHADER]
 
[FRAGMENTSHADER] 
	NAME 		GBufferFragmentShader 

	[GLSL_CODE]
		#version 300 es

		uniform  sampler2D  sTexture;
		uniform  sampler2D  sBumpMap;

		uniform  highp float  uFarClipDistance;
		uniform  lowp  float  uSpecularPower;
		uniform  lowp  vec3   uDiffuseColour;

		in  mediump vec2   vTexCoord;
		in  highp   vec3   vNormal;
		in  highp   vec3   vTangent;
		in  highp   vec3   vBinormal;
		in  highp   vec3   vViewPos;
		
		layout(location = 0) out lowp  vec4  oAlbedo; 
		layout(location = 1) out highp vec3  oNormal; 
		layout(location = 2) out highp vec4  oDepth; 

		void main() 
		{
			// Calculate the albedo
			lowp vec3 albedo = texture(sTexture, vTexCoord).rgb * uDiffuseColour;
			// Pack the specular exponent with the albedo
			oAlbedo = vec4(albedo, uSpecularPower);

			// Calculate viewspace perturbed normal
			highp vec3 bumpmap = normalize(texture(sBumpMap, vTexCoord).rgb * 2.0 - 1.0);
			highp mat3 tangentSpace = mat3(normalize(vTangent), normalize(vBinormal), normalize(vNormal));	
			highp vec3 normalVS = tangentSpace * bumpmap;		

			// Scale the normal range from [-1,1] to [0, 1] to pack it into the RGB_U8 texture
			oNormal.xyz = normalVS * 0.5 + 0.5;	

			// Pack the depth value into 4 channels 	
			// Pack the depth value into 4 channels 	
			highp float scaledDepth = length(vViewPos) / uFarClipDistance;
			highp vec4 enc = vec4(1.0, 255.0, 65025.0, 160581375.0) * scaledDepth;
			enc = fract(enc);
			enc -= enc.yzww * vec4(1.0/255.0,1.0/255.0,1.0/255.0,0.0);			
			
			oDepth = enc;			
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]



///////////////////////////////////////////////////////////////////////////
////                                                                   ////
////                   DEFERRED LIGHT SHADERS                          ////
////                                                                   ////
///////////////////////////////////////////////////////////////////////////

// 
// Vertex shader that is shared amongst (most) deferred light shaders
//
[VERTEXSHADER]
	NAME 		DeferredVertexShader

	[GLSL_CODE]
		#version 300 es

		#define VERTEX_ARRAY      0

		layout(location = VERTEX_ARRAY)   in highp   vec3 inVertex;

		uniform  mat4  uWorldViewProjMatrix;
		uniform  mat4  uWorldViewMatrix;
		
		out  highp   vec3  vPositionVS;
		out  highp   vec3  vViewDirVS;
		out  mediump vec2  vTexCoord;

		void main() 
		{ 	
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
			gl_Position.xyz = gl_Position.xyz / gl_Position.w;
			gl_Position.w = 1.0;
			
			vTexCoord = (gl_Position.xy + 1.0) * 0.5;
	
			// Calculate the view-space position for lighting calculations
			vPositionVS = (uWorldViewMatrix * vec4(inVertex, 1.0)).xyz;

			// Pass the view direction
			vViewDirVS = vPositionVS;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

//
// Calculates the lighting for a point light source
//
[EFFECT] 
	NAME 	RenderPointLight
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix		WORLDVIEWPROJECTION
	UNIFORM uWorldViewMatrix			WORLDVIEW
	UNIFORM uWorldIT					WORLDIT
	UNIFORM uLightColourIntensity		LIGHTCOLOR
		
	UNIFORM sAlbedo						TEXTURE0
	UNIFORM sNormals					TEXTURE1
	UNIFORM sDepth						TEXTURE2
	UNIFORM sLightEnvMap				TEXTURE3
	
	// CUSTOM SEMANTICS
	UNIFORM uFarClipDistance			CUSTOMSEMANTIC_FARCLIPDISTANCE
	
	UNIFORM uLightViewPosition		CUSTOMSEMANTIC_POINTLIGHT_VIEWPOSITION
	
	// ATTRIBUTES
	ATTRIBUTE inVertex			POSITION

	// SHADERS	
	VERTEXSHADER   DeferredVertexShader
	FRAGMENTSHADER PointLightFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		PointLightFragmentShader 

	[GLSL_CODE]
		#version 300 es

		uniform  sampler2D    sAlbedo;
		uniform  sampler2D    sNormals;
		uniform  sampler2D    sDepth;
		uniform  samplerCube  sLightEnvMap;

		uniform  highp float  uFarClipDistance;
		uniform  highp vec3   uLightColourIntensity;

		uniform  highp mat3   uWorldIT;		
		uniform  highp vec3   uLightViewPosition;
		
		in  highp   vec3  vPositionVS;
		in  highp   vec3  vViewDirVS;
		in  mediump vec2  vTexCoord;

		layout(location = 0) out lowp vec4 oColour;

		void main()
		{
			//
			// Read GBuffer attributes
			//	
			highp vec4 depthTex = texture(sDepth, vTexCoord);
			highp vec4 albedoSpec = texture(sAlbedo, vTexCoord);
			highp vec3 normalTex = texture(sNormals, vTexCoord).xyz;
			
			// reconstruct original depth value
			highp float depth = dot(depthTex, vec4(1.0, 1.0/255.0, 1.0/65025.0, 1.0/160581375.0));

			//
			// Reconstruct common vectors and world space position 
			//
			highp vec3 positionVS = normalize(vPositionVS) * depth * uFarClipDistance;	
			highp vec3 lightDirection = uLightViewPosition - positionVS;
			lowp  vec3 lightColour = texture(sLightEnvMap, uWorldIT * lightDirection).rgb;
			highp float invLightDistance = 1.0 / length(lightDirection);
			lightDirection *= invLightDistance;
	
			//
			// Calculate lighting terms
			//
			highp vec3 normal = normalize(normalTex * 2.0 - 1.0);	
			highp float n_dot_l = max(dot(lightDirection, normal), 0.0);	
			highp vec3 diffuse = n_dot_l * albedoSpec.rgb;

			highp vec3 viewDirection = normalize(vViewDirVS);
			highp vec3 reflectedLightDirection = reflect(lightDirection, normal);
			highp float v_dot_r = max(dot(viewDirection, reflectedLightDirection), 0.0);
			diffuse += vec3(pow(v_dot_r, albedoSpec.a * 100.0));
	
			highp float attenuation = invLightDistance * invLightDistance;
			oColour = vec4(diffuse * uLightColourIntensity * attenuation * lightColour, 1.0);
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

//
// Calculates the lighting for a directional light source
//
[EFFECT] 
	NAME 	RenderDirectionalLight
		
	// GLOBALS UNIFORMS
	UNIFORM uLightColourIntensity		LIGHTCOLOR
			
	UNIFORM sAlbedo						TEXTURE0
	UNIFORM sNormals					TEXTURE1
		
	// CUSTOM SEMANTICS
	UNIFORM uLightDirection			CUSTOMSEMANTIC_DIRECTIONALLIGHT_DIRECTION
	
	// ATTRIBUTES
	ATTRIBUTE inVertex			POSITION
	ATTRIBUTE inTexCoord			UV

	// SHADERS	
	VERTEXSHADER   DirectionalLightVertexShader
	FRAGMENTSHADER DirectionalLightFragmentShader
[/EFFECT]

[VERTEXSHADER]
	NAME 		DirectionalLightVertexShader

	[GLSL_CODE]
		#version 300 es

		#define VERTEX_ARRAY      0
		#define NORMAL_ARRAY      1
		#define TEXCOORD_ARRAY    2

		layout(location = VERTEX_ARRAY)   in highp   vec3 inVertex;
		layout(location = TEXCOORD_ARRAY) in mediump vec2 inTexCoord;

		uniform  highp vec4  uLightDirection;

		out  highp   vec3  vLightDirection;
		out  mediump vec2  vTexCoord;

		void main() 
		{ 	
			gl_Position = vec4(inVertex, 1.0);

			vTexCoord = inTexCoord;
			vLightDirection = uLightDirection.xyz;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

[FRAGMENTSHADER] 
	NAME 		DirectionalLightFragmentShader 

	[GLSL_CODE]
		#version 300 es

		uniform  sampler2D  sAlbedo;
		uniform  sampler2D  sNormals;

		uniform highp vec3  uLightColourIntensity;

		in highp   vec3 vLightDirection;
		in mediump vec2 vTexCoord;

		layout(location = 0) out lowp vec4 oColour;

		void main()
		{
			// Fetch required gbuffer attributes
			lowp  vec3 albedo = texture(sAlbedo, vTexCoord).rgb;
			highp vec3 normalTex = texture(sNormals, vTexCoord).xyz;
			highp vec3 normal = normalize(normalTex.xyz * 2.0 - 1.0);
			
			// Calculate simple diffuse lighting
			highp float n_dot_l = max(dot(-vLightDirection, normal.xyz), 0.0);
			lowp vec3 colour = albedo * n_dot_l * uLightColourIntensity;
			
			oColour = vec4(colour, 1.0);
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

///////////////////////////////////////////////////////////////////////////
////                                                                   ////
////                   AUXILIARY SHADERS                               ////
////                                                                   ////
///////////////////////////////////////////////////////////////////////////

//
// Renders the geometry using a texture
//
[EFFECT] 
	NAME 	RenderSimpleTexture
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix		WORLDVIEWPROJECTION
	
	UNIFORM sTexture					TEXTURE0
	
	// ATTRIBUTES
	ATTRIBUTE inVertex					POSITION
	ATTRIBUTE	inTexCoord				UV	

	// SHADERS	
	VERTEXSHADER   VertexShader
	FRAGMENTSHADER TextureFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		TextureFragmentShader 

	[GLSL_CODE]
		#version 300 es

		uniform sampler2D sTexture;
		in mediump vec2 vTexCoord;

		layout(location = 0) out lowp vec4 oColour;

		void main()
		{
			oColour = texture(sTexture, vTexCoord);			
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]


//
// Renders the geometry using a single channel depth map, 
// scaling the depth values for visualization purposes.
//
[EFFECT] 
	NAME 	RenderDepthTexture
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	
	UNIFORM sTexture				TEXTURE0
	
	// ATTRIBUTES
	ATTRIBUTE	inVertex			POSITION
	ATTRIBUTE	inTexCoord			UV	

	// SHADERS	
	VERTEXSHADER   VertexShader
	FRAGMENTSHADER DepthTextureFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		DepthTextureFragmentShader 

	[GLSL_CODE]
	#version 300 es

		uniform sampler2D sTexture;
		in mediump vec2 vTexCoord;

		layout(location = 0) out lowp vec4 oColour;

		void main()
		{
			highp vec4 depthTex = texture(sTexture, vTexCoord);
			
			// reconstruct original depth value
			highp float depth = dot(depthTex, vec4(1.0, 1.0/255.0, 1.0/65025.0, 1.0/160581375.0));
			
			oColour = vec4(vec3(texture(sTexture, vTexCoord).r * 5.0), 1.0);
			
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]


//
// Renders the geometry using a single colour passed as an uniform.
//
[EFFECT] 
	NAME 	RenderSolidColour
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uConstantColour			MATERIALCOLORAMBIENT
	
	// ATTRIBUTES
	ATTRIBUTE	inVertex			POSITION
	ATTRIBUTE	inTexCoord			UV

	// SHADERS	
	VERTEXSHADER   VertexShader
	FRAGMENTSHADER SolidColourFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		SolidColourFragmentShader 

	[GLSL_CODE]
		#version 300 es

		uniform lowp vec4 uConstantColour;

		layout(location = 0) out lowp vec4 oColour;

		void main()
		{
			oColour = uConstantColour;
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

[VERTEXSHADER]
	NAME 		VertexShader

	[GLSL_CODE]
		#version 300 es

		#define VERTEX_ARRAY      0
		#define NORMAL_ARRAY      1
		#define TEXCOORD_ARRAY    2

		layout(location = VERTEX_ARRAY)   in highp   vec3 inVertex;
		layout(location = TEXCOORD_ARRAY) in mediump vec2 inTexCoord;

		uniform  mat4  uWorldViewProjMatrix; 

		out mediump vec2 vTexCoord;

		void main() 
		{ 
			// pass-through position and texture coordinates
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
			vTexCoord = inTexCoord;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]

//
// Renders the geometry using a texture
//
[EFFECT] 
	NAME 	RenderCubeTexture
		
	// GLOBALS UNIFORMS
	UNIFORM uWorldViewProjMatrix	WORLDVIEWPROJECTION
	UNIFORM uWorldIT				WORLDIT
	UNIFORM uColour					MATERIALCOLORAMBIENT
	
	UNIFORM sTexture				TEXTURE0
	
	// ATTRIBUTES
	ATTRIBUTE	inVertex			POSITION
	ATTRIBUTE	inNormal			NORMAL
	ATTRIBUTE	inTexCoord			UV

	// SHADERS	
	VERTEXSHADER   CubeTextureVertexShader
	FRAGMENTSHADER CubeTextureFragmentShader
[/EFFECT]

[FRAGMENTSHADER] 
	NAME 		CubeTextureFragmentShader 

	[GLSL_CODE]
		#version 300 es

		uniform samplerCube sTexture;		
		uniform lowp vec4 uColour;
		
		in mediump vec3 vNormal;
		
		layout(location = 0) out lowp vec4 oColour;

		void main()
		{
			oColour = texture(sTexture, vNormal) * uColour;
		}
	[/GLSL_CODE]
[/FRAGMENTSHADER]

[VERTEXSHADER]
	NAME 		CubeTextureVertexShader

	[GLSL_CODE]
		#version 300 es

		#define VERTEX_ARRAY      0
		#define NORMAL_ARRAY      1
		#define TEXCOORD_ARRAY    2

		layout(location = VERTEX_ARRAY)   in highp	vec3 inVertex;
		layout(location = NORMAL_ARRAY)   in highp	vec3 inNormal;

		uniform  highp mat4  uWorldViewProjMatrix; 
		uniform  highp mat3  uWorldIT;

		out mediump vec3 vNormal;

		void main() 
		{ 
			// pass-through position and texture coordinates
			gl_Position = uWorldViewProjMatrix * vec4(inVertex, 1.0);
			vNormal = uWorldIT * inNormal;
		} 
	[/GLSL_CODE]
[/VERTEXSHADER]